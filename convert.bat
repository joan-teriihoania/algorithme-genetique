@echo off

set input_filename=%~1
set output_filename=%~2

set temp_dir=rapport_temp
set latex_filename=Latex.tex
set temp_latex_filename=%temp_dir%/%latex_filename%
set exit_code=0
echo.

if "%~1" equ "/?" (
  call :help
  exit /b
)

set command_needed=0
WHERE pdflatex >nul 2>nul
IF %ERRORLEVEL% NEQ 0 (
  echo [ERRO] The command 'pdflatex' is not installed.
  set command_needed=1
)

WHERE pandoc >nul 2>nul
IF %ERRORLEVEL% NEQ 0 (
  echo [ERRO] The command 'pandoc' is not installed.
  set command_needed=1
)

WHERE py >nul 2>nul
IF %ERRORLEVEL% NEQ 0 (
  echo [ERRO] The command 'py' is not installed.
  set command_needed=1
)

if %command_needed% neq 0 (
  echo [INFO] Make sure to install the previous command^(s^).
  echo [INFO] If the command^(s^) are installed, contact the developer.
  set exit_code=6
  goto exit
)

if not exist "%input_filename%" (
  echo [ERRO]  Input file "%input_filename%" could not be found in directory.
  echo [ERRO]  Please check if the file exists or is in the active directory.
  set exit_code=1
  goto exit
)

if not exist "%temp_dir%" (
  md "%temp_dir%"
)

if exist "%temp_latex_filename%" (
  cd %temp_dir%
  del /q "%latex_filename%" > nul
  cd ..
)

echo [INFO] Converting markdown to Latex...
pandoc -f gfm -t latex "%input_filename%">"%temp_latex_filename%"
if not exist "%temp_latex_filename%" (
  echo [ERRO]  Conversion to LATEX failed
  set exit_code=2
  goto exit
)

echo [INFO] Reformatting Latex file...
py format.py "%temp_latex_filename%"

echo [INFO] Converting to PDF...
if exist "%output_filename%.pdf" if "%~3" neq "--overwrite" (
  echo [ERRO]  File already named as "%output_filename%.pdf" found
  echo [ERRO]  Use '--overwrite' option to overwrite the existing file.
  set exit_code=3
  goto exit
)

if not exist "%temp_dir%/Main.tex" (
  echo [INFO] Downloading template file...
  powershell -Command "Invoke-WebRequest http://joan-teriihoania.fr/updater/Main.tex -OutFile %temp_dir%/Main.tex" > nul
  if not exist "%temp_dir%/Main.tex" echo [ERRO]  Download failed
  if not exist "%temp_dir%/Main.tex" set exit_code=5
  if not exist "%temp_dir%/Main.tex" goto exit
  echo [INFO] Download complete
)

if exist "%output_filename%.pdf" del /q "%output_filename%.pdf" > nul
pdflatex "%temp_dir%/Main.tex" --job-name="%output_filename%" --aux-directory="%temp_dir%" > nul

if not exist "%output_filename%.pdf" (
  echo [ERRO]  Conversion to PDF failed
  set exit_code=4
  goto exit
)

echo [INFO] Updating TOC from PDF...
del /q "%output_filename%.pdf" > nul
pdflatex "%temp_dir%/Main.tex" --job-name="%output_filename%" --aux-directory="%temp_dir%" > nul

if not exist "%output_filename%.pdf" (
  echo [ERRO]  Conversion to PDF failed
  set exit_code=4
  goto exit
)

:exit
echo [EXIT] Process ended with exit code %exit_code%
if %exit_code% neq 0 (
  echo [EXIT] PROCESS EXECUTION ENDED WITH ERRORS
  echo [EXIT] Use '/?' option to display help.
)
echo.
exit /b

:help

  echo This command will convert using pandoc utility tool to convert any given github
  echo markdown input file to a latex, not pdf, output file named as given in parameters.
  echo This command also use powershell command to reformat and replace includegraphics
  echo tags for better display of the images generated by the pandoc command.
  echo Then, it will the convert the resulting Latex file to PDF using pdflatex from Miktex.
  echo.
  echo Syntax: convert ^<Input filename^> ^<Output filename^> [options]
  echo   Input filename  : Name of the input file.
  echo   Output filename : Name of the file which will receive the result of the
  echo                     conversion without extension.
  echo.
  echo Options :
  echo   --overwrite     : Option to overwrite, disabled by default, the contentof any file
  echo                     named as the given output filename with the result of the conversion.
  echo.
  echo Error codes index:
  echo   [0] The process ended without any errors.
  echo   [1] The process couldn't find the given input filename.
  echo   [2] The conversion failed to LATEX from GITHUB MARKDOWN or the outputed result/file
  echo       from the conversion couldn't be found by the process.
  echo   [3] The process found a file already named as the given output PDF filename.
  echo   [4] The conversion failed to PDF from LATEX or the outputed result/file
  echo       from the conversion couldn't be found by the process.
  echo   [5] The Main.tex file used for the generation of PDF could not be found and downloaded.
  echo   [6] One the following required command is not installed :
  echo          - pandoc (https://pandoc.org/)
  echo          - pdflatex (https://miktex.org/)
  echo          - py (https://www.python.org/)
goto:eof